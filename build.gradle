apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply from: 'gradle/plugins/javafx.plugin'

group = 'org.hildan'
version = '3.1.0'
sourceCompatibility = 1.8
description = 'A JavaFX program to view log files in a beautiful way.'
ext.labels = ['log', 'viewer']

def mainClassName = "org.hildan.fxlog.FXLog"

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.controlsfx:controlsfx:8.40.13'
    compile 'org.fxmisc.easybind:easybind:1.0.3'
    compile 'commons-io:commons-io:2.5'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'org.hildan.fxgson:fx-gson:3.1.0'
    compile 'org.jetbrains:annotations:15.0'
    compile 'ch.qos.logback:logback-classic:1.0.13'
    testCompile 'junit:junit:4.+'
}

// to allow for live recompilation of only modified sources using -t option
compileJava {
    options.incremental = true
}

checkstyle {
    maxWarnings = 0
    toolVersion = '7.6'
}

// to inject the project version in about.fxml and .properties files
processResources {
    def today = getFormattedDate();
    inputs.property "version", project.version
    inputs.property "date", today
    ['**/*.properties', '**/about.fxml'].each {
        filesMatching(it) {
            f -> f.expand(projectVersion: project.version, buildDate: today)
        }
    }
}

static String getFormattedDate() {
    return new Date().format('MMM d, yyyy')
}

// to build a fat jar with all dependencies included
jar {
    manifest { attributes "Main-Class": mainClassName }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

// defines JavaFX deployment info
javafx {
    appID project.name
    appName project.name
    mainClass mainClassName

    category = 'Tools'
    copyright = 'Copyright (c) 2016 Joffrey Bion'
    description = project.description
    licenseType = 'MIT'
    vendor = "Joffrey Bion"
    installSystemWide = true
    menu = true
    shortcut = true
}

apply from: 'gradle/scripts/bintray.gradle'
apply from: 'gradle/scripts/release.gradle'

release.dependsOn(bintrayUpload)

